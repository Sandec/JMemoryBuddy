buildscript {
  repositories {
    jcenter()
  }
}

repositories {
  jcenter()
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'


group = 'de.sandec'
archivesBaseName = "JMemoryBuddy"
version = '0.5.2'

sourceCompatibility = 8
targetCompatibility = 8

java {
  withSourcesJar()
  withJavadocJar()
}

dependencies {
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
  implementation 'org.openjdk.jol:jol-core:0.16'
}

test {
  testLogging {
    events "passed", "skipped", "failed"
  }
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId project.group
      artifactId "JMemoryBuddy"
      version project.version


      pom {
        packaging = 'jar'
        // optionally artifactId can be defined here
        description = 'A library usefull for unit testing memory leaks'
        url = 'https://github.com/Sandec/jmemorybuddy'

        scm {
          connection = 'https://github.com/Sandec/JMemoryBuddy.git'
          developerConnection = 'https://github.com/Sandec/JMemoryBuddy.git'
          url = 'https://github.com/Sandec/JMemoryBuddy.git'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'floriankirmaier'
            name = 'Florian Kirmaier'
            email = 'florian.kirmaier@gmail.com'
          }
        }
      }
    }
  }
}

jar {
  manifest {
    attributes  'Automatic-Module-Name': "de.sandec.jmemorybuddy"
  }
}

if (project.hasProperty('ossrhUsername')) {
  signing {
    //def signingKey = findProperty("signing.secretKeyRingFile")
    //def signingPassword = findProperty("signing.password")
    //useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
  }

  publishing {
    repositories {
      maven {
        name = "artifactory"
        url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        // SNAPSHOTS
        //url = "https://oss.sonatype.org/content/repositories/snapshots/"
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
      }
    }
  }

}
