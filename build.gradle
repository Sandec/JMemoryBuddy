plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'tech.yanand.maven-central-publish' version "$MAVEN_CENTRAL_PUBLISH_PLUGIN_VERSION"
}

group = 'one.jpro'
version = "$JMEMORYBUDDY_VERSION"

java {
    sourceCompatibility = 11
    targetCompatibility = 11

    // Sonatype officially requires Java source and Java doc
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:$JUNIT_VERSION"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$JUNIT_VERSION"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$JUNIT_VERSION"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$JUNIT_VERSION"
    testRuntimeOnly "ch.qos.logback:logback-classic:$LOGBACK_VERSION"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        showExceptions true
        showCauses true
        showStackTraces true
        exceptionFormat "full"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name.toLowerCase()
            version project.version

            pom {
                name = project.name
                description = 'A library usefull for unit testing memory leaks'
                url = 'https://github.com/Sandec/JMemoryBuddy'

                scm {
                    connection = 'https://github.com/Sandec/JMemoryBuddy.git'
                    developerConnection = 'https://github.com/Sandec/JMemoryBuddy.git'
                    url = 'https://github.com/Sandec/JMemoryBuddy'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'floriankirmaier'
                        name = 'Florian Kirmaier'
                        email = 'florian.kirmaier@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "artifactory"
            url = "https://sandec.jfrog.io/artifactory/repo"
            credentials {
                username System.getenv("SANDEC_ARTIFACTORY_USERNAME")
                password System.getenv("SANDEC_ARTIFACTORY_PASSWORD")
            }
        }
        maven {
            name = "staging-deploy"
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

signing {
    useInMemoryPgpKeys (
            System.getenv("SANDEC_SIGNING_KEY_ID"),
            System.getenv("SANDEC_SIGNING_SECRET_KEY"),
            System.getenv("SANDEC_SIGNING_PASSWORD")
    )

    sign publishing.publications.mavenJava
}

mavenCentral {
    repoDir = layout.buildDirectory.dir('staging-deploy')
    authToken = System.getenv("SANDEC_SONATYPE_AUTH_TOKEN")
    publishingType = 'USER_MANAGED'
}
