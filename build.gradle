buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
  }
}

repositories {
  jcenter()
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'


group = 'de.sandec'
archivesBaseName = "JMemoryBuddy"
version = '0.5.1-SNAPSHOT'

sourceCompatibility = 8
targetCompatibility = 8

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
artifacts {
  archives javadocJar, sourcesJar
}

dependencies {
  testImplementation 'junit:junit:4.13.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

test {
  testLogging {
    events "passed", "skipped", "failed"
  }
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId project.group
      artifactId "JMemoryBuddy"
      version project.version
    }
  }
}

jar {
  manifest {
    attributes  'Automatic-Module-Name': "de.sandec.jmemorybuddy"
  }
}

if (project.hasProperty('ossrhUsername')) {
    signing {
        sign configurations.archives
    }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'JMemoryBuddy'
          packaging 'jar'
          // optionally artifactId can be defined here
          description 'A library usefull for unit testing memory leaks'
          url 'https://github.com/Sandec/jmemorybuddy'

          scm {
            connection 'https://github.com/Sandec/JMemoryBuddy.git'
            developerConnection 'https://github.com/Sandec/JMemoryBuddy.git'
            url 'https://github.com/Sandec/JMemoryBuddy.git'
          }

          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id 'floriankirmaier'
              name 'Florian Kirmaier'
              email 'florian.kirmaier@gmail.com'
            }
          }
        }
      }
    }
  }
}


bintray {
  if(project.hasProperty("BINTRAY_USER")) {
    user = BINTRAY_USER
    key  = BINTRAY_KEY
  }
  publications = ['mavenJava']
  publish = true
  override = true
  pkg {
    repo = 'repo'
    name = project.name
    userOrg = 'sandec'
  }
}